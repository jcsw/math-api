buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
        avastDockerComposeVersion = '0.7.1'
        transmodeDockerVersion = '1.2'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.avast.gradle:gradle-docker-compose-plugin:$avastDockerComposeVersion"
        classpath "se.transmode.gradle:gradle-docker:$transmodeDockerVersion"
    }
}

apply plugin: 'java'

repositories {
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

group = 'br.com.jcsw'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 10
targetCompatibility = 10

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    ext {
        springBootVersion = '2.0.2.RELEASE'
        springCloudVersion = 'Finchley.BUILD-SNAPSHOT'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
        }
    }

    dependencies {
        compile('org.springframework.cloud:spring-cloud-starter-netflix-hystrix')
        compile('org.springframework.cloud:spring-cloud-starter-netflix-ribbon')
        compile('org.springframework.cloud:spring-cloud-starter-openfeign')

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile 'io.cucumber:cucumber-java8:3.0.2'
        testCompile 'io.cucumber:cucumber-junit:3.0.2'
    }

    jacoco {
        toolVersion = "0.7.9"
    }
}

project('application-ws') {
    apply plugin: 'application'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'docker'
    apply plugin: 'docker-compose'

    mainClassName = 'br.com.jcsw.math.MathApiApplication'

    dockerCompose {
        useComposeFiles = ["$project.rootDir/docker/dependencies/mongodb/docker-compose.yml", "$project.rootDir/docker/dependencies/rabbitmq/docker-compose.yml"]
        stopContainers = false
    }

    dockerCompose.isRequiredBy(bootRun)

    distDocker {
        baseImage "openjdk:10-jdk-slim"
        maintainer 'Jonas Cezar "jcezar.al@gmail.com"'
        tag = "jcezar/math-api"
        exposePort 9900
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('io.springfox:springfox-swagger2:2.8.0')
        compile('io.springfox:springfox-swagger-ui:2.8.0')

        compile project(':domain-api')
        compile project(':spring-aop-log')
        runtime project(':domain-impl')

        runtime('org.springframework.boot:spring-boot-devtools')

        testCompile('org.springframework.boot:spring-boot-starter-amqp')
        testCompile('org.springframework.boot:spring-boot-starter-data-mongodb')

        testCompile project(':infra-mongodb')
    }
}

project('domain-api') {
    dependencies {
        compile('org.apache.commons:commons-lang3')
        compile('io.springfox:springfox-swagger2:2.8.0')
    }
}

project('domain-impl') {
    dependencies {
        compile project(':domain-api')
        compile project(':spring-aop-log')
        compile project(':infra-mongodb')
        compile project(':infra-rabbitmq')
        compile project(':infra-feature-toggle')
    }
}

project('infra-feature-toggle') {

    configurations {
        integrationtestCompile.extendsFrom testCompile
        integrationtestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationtest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationtest/java')
            }
            resources.srcDir file('src/integrationtest/resources')
        }
    }

    task integrationtest(type: Test) {
        testClassesDirs = sourceSets.integrationtest.output.classesDirs
        classpath = sourceSets.integrationtest.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        compile project(':spring-aop-log')

        testCompile('com.github.tomakehurst:wiremock:2.18.0')
    }
}

project('infra-mongodb') {

    apply plugin: 'docker-compose'

    dockerCompose {
        useComposeFiles = ["$project.projectDir/src/integrationtest/resources/docker-compose.yml"]
        stopContainers = true
        removeContainers = true
    }

    configurations {
        integrationtestCompile.extendsFrom testCompile
        integrationtestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationtest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationtest/java')
            }
            resources.srcDir file('src/integrationtest/resources')
        }
    }

    task integrationtest(type: Test) {
        testClassesDirs = sourceSets.integrationtest.output.classesDirs
        classpath = sourceSets.integrationtest.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dockerCompose.isRequiredBy(integrationtest)

    dependencies {
        compile project(':spring-aop-log')
        compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    }
}

project('infra-rabbitmq') {

    apply plugin: 'docker-compose'

    dockerCompose {
        useComposeFiles = ["$project.projectDir/src/integrationtest/resources/docker-compose.yml"]
        stopContainers = true
        removeContainers = true
    }

    configurations {
        integrationtestCompile.extendsFrom testCompile
        integrationtestRuntime.extendsFrom testRuntime
    }

    sourceSets {
        integrationtest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationtest/java')
            }
            resources.srcDir file('src/integrationtest/resources')
        }
    }

    task integrationtest(type: Test) {
        testClassesDirs = sourceSets.integrationtest.output.classesDirs
        classpath = sourceSets.integrationtest.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dockerCompose.isRequiredBy(integrationtest)

    dependencies {
        compile project(':spring-aop-log')
        compile('org.springframework.boot:spring-boot-starter-amqp')
    }
}